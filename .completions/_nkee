#compdef nkee

_nkee() {
  local curcontext="$curcontext" state
  local -a mainopts

  mainopts=(
  'info:Display info about an ExpressionEngine instance'
  'install:Install ExpressionEngine'
  )
  _arguments -C \
    '1: :->command'\
    '*: :->args'

  if [[ "$state" == "args" ]]; then
    case "${words[2]}" in
      "info")
        _arguments \
          '-S[Path to the system directory]' \
          '-I[EE site ID Default value: 1]' \
          '*:ExpressionEngine Directory:_path_files -/'
        ;;

      "install")
        _arguments \
          '--admin-password[Specify an initial password for the admin user]' \
          '--admin-username[Specify the username for the default admin user]' \
          '(-e --admin-email)'{-e,--admin-email}'[Admin userâ€™s email address]' \
          '(-y --assume-yes)'{-y,--assume-yes}'[Assume yes at all prompts]' \
          '(-a --auto-create-database)'{-a,--auto-create-database}'[Automatically create the database and user]' \
          '(-b --base-url)'{-b,--base-url}'[Base URL to use for ExpressionEngine]' \
          '(-c --system-url)'{-c,--system-url}'[URL to use for ExpressionEngine control Panel]' \
          '(-l --license-key)'{-l,--license-key}'[ExpressionEngine license key]' \
          '(-L --site-label)'{-L,--site-label}'[Initial title for the ExpressionEngine instance]' \
          '(-p --package)'{-p,--package}'[ExpressionEngine version to install, defaults to "latest]' \
          '(-P --system-path)'{-P,--system-path}'[Path for the ExpressionEngine system directory]' \
          '--database-host[Database host]' \
          '(-N --database-name)'{-N,--database-name}'[Database name]' \
          '--database-pass[Database password]' \
          '(-U --database-user)'{-U,--database-user}'[Database username]' \
          '*:ExpressionEngine Directory:_path_files -/'
        ;;

      "*")
        ;;
    esac
  else
    _describe -t commands " Sub commands " mainopts
  fi
}
compdef _nkee nkee
