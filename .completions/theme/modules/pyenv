# vim:ft=bash
_riff_read_pyenv_version_file() {
  [[ -r $1 ]] || return
  local content
  read -rd $'\0' content <$1 2>/dev/null
  _RIFF_RETVAL=${${(j.:.)${(@)${=content}#python-}:-system}}
}

_riff_pyenv_global_version() {
  _riff_read_pyenv_version_file ${PYENV_ROOT:-$HOME/.pyenv}/version || _RIFF_RETVAL=system
}

################################################################
# Segment to display pyenv information
# https://github.com/pyenv/pyenv#choosing-the-python-version
prompt_pyenv() {
  local v=${(j.:.)${(@)${(s.:.)PYENV_VERSION}#python-}}
  if [[ -z $v ]]; then
    [[ $PYENV_DIR == /* ]] && local dir=$PYENV_DIR || local dir="$PWD/$PYENV_DIR"
    while true; do
      if _riff_read_pyenv_version_file $dir/.python-version; then
        v=$_RIFF_RETVAL
        break
      fi
      if [[ $dir == / ]]; then
        [[ $RIFF_PYENV_PROMPT_ALWAYS_SHOW == true ]] || return
        _riff_pyenv_global_version
        v=$_RIFF_RETVAL
        break
      fi
      dir=${dir:h}
    done
  fi

  if [[ $RIFF_PYENV_PROMPT_ALWAYS_SHOW == false ]]; then
    _riff_pyenv_global_version
    [[ $v == $_RIFF_RETVAL ]] && return
  fi

  "$1_prompt_module" "$0" "$2" "blue" "$DEFAULT_COLOR" 'PYTHON_ICON' 0 '' "${v//\%/%%}"
}
