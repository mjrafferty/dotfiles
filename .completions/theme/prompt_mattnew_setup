# vim:ft=bash

_riff_installation_dir="$HOME/.completions/theme"

## Configurables
typeset -g  RIFF_LEFT_SEPARATOR
typeset -g  RIFF_RIGHT_SEPARATOR
typeset -ga RIFF_LEFT_PROMPT_ELEMENTS
typeset -ga RIFF_RIGHT_PROMPT_ELEMENTS

RIFF_LEFT_SEPARATOR=" "
RIFF_LEFT_SEPARATOR=" "
RIFF_LEFT_PROMPT_ELEMENTS=(user context php_version)
RIFF_RIGHT_PROMPT_ELEMENTS=(command_execution_time date)

## Necessary global variables
typeset -g _RIFF_EXIT_CODE        # Capture command exit code
typeset -g _RIFF_RETURN_MESSAGE   # Used as a temporary storage for values to avoid using subshells

source "${_riff_installation_dir}/lib/colors.zsh"

_riff_set_module_text() {

  local module module_text fg_color bg_color
  local -u config # Convert module name to upper case when checking config

  module="$1"
  config="RIFF_${module}"

  "_riff_${module}_run"
  module_text="${_RIFF_RETURN_MESSAGE}"

  _riff_translate_color "${${(P)config}_FG}"
  fg_color="${_RIFF_RETURN_MESSAGE}"

  _riff_foreground "${fg_color}"
  fg_color="${_RIFF_RETURN_MESSAGE}"

  _riff_translate_color "${${(P)config}_BG}"
  bg_color="${_RIFF_RETURN_MESSAGE}"

  _riff_background "${bg_color}"
  bg_color="${_RIFF_RETURN_MESSAGE}"

  _RIFF_RETURN_MESSAGE="${fg_color}${bg_color}${module_text}"

}

_riff_get_module_text() {

  local module

  module="$1"

  # if has cache; then
  #   TODO
  # else
  _riff_set_module_text "$module"
  # fi

}

_riff_set_prompt(){

  local element

  for ((element=1; element<=${#RIFF_LEFT_PROMPT_ELEMENTS[@]};element++)); do

    _riff_get_module_text "${RIFF_LEFT_PROMPT_ELEMENTS[element]}"

    if ((element > 1)); then
      PROMPT+="${RIFF_LEFT_SEPARATOR}"
    fi

    PROMPT+="${_RIFF_RETURN_MESSAGE}"

  done

  PROMPT+="%f%k "

  for ((element=1; element<=${#RIFF_RIGHT_PROMPT_ELEMENTS[@]};element++)); do

    _riff_get_module_text "${RIFF_RIGHT_PROMPT_ELEMENTS[element]}"

    if ((element > 1)); then
      RPROMPT+="${RIFF_RIGHT_SEPARATOR}"
    fi

    RPROMPT+="${_RIFF_RETURN_MESSAGE}"

  done

  RPROMPT+="%f%k"

}

_riff_precmd() {

  _RIFF_EXIT_CODE=$?
  #_RIFF_PIPE_EXIT_CODES=( "$pipestatus[@]" )

  PROMPT=""
  RPROMPT=""

  _riff_set_prompt

}

_riff_preexec() {

  # Run preexec hooks for modules
  for element in "${RIFF_LEFT_PROMPT_ELEMENTS[@]}" "${RIFF_RIGHT_PROMPT_ELEMENTS[@]}"; do
    if typeset -f "_riff_${element}_module_preexec" > /dev/null; then
      "_riff_${element}_module_preexec";
    fi
  done

}

prompt_riff_setup() {

  local element

  autoload -U colors && colors
  autoload -Uz add-zsh-hook

  zmodload zsh/datetime
  zmodload zsh/mathfunc
  zmodload zsh/system
  zmodload -F zsh/stat b:zstat

  add-zsh-hook precmd _riff_precmd
  add-zsh-hook preexec _riff_preexec

  #_riff_init_icons

  # Load enabled modules and run setup hooks
  for element in "${RIFF_LEFT_PROMPT_ELEMENTS[@]}" "${RIFF_RIGHT_PROMPT_ELEMENTS[@]}"; do

    if [[ -f "${_riff_installation_dir}/modules/${element}" ]]; then

      source "${_riff_installation_dir}/modules/${element}";

      if typeset -f "_riff_${element}_module_setup" > /dev/null; then
        "_riff_${element}_module_setup";
      fi

    fi

  done

}

prompt_riff_setup
