#compdef nkmagento

_nkmagento() {
  local curcontext="$curcontext" state
  local -a mainopts

  mainopts=(
  'info:Display info about a Magento site'
  'install:Install Magento'
  'repair:Repair Magento'
  'copy:Copy Magento'
  'link:Create a Magento storefront'
  'dbrepair:Run the Magento DB Repair tool'
  'backup:Create a Magento backup'
  'crondump:Dump the Magento internal cron status'
  );

  _arguments -C \
    '1: :->command'\
    '*: :->args'

  if [[ "$state" == "args" ]]; then
    case "${words[2]}" in
      "info")
        _arguments \
          '*:Path to the Magento site:_path_files -/'
        ;;

      "install")
        _arguments \
          '(-E --enterprise)'{-E,--enterprise}'[Install Magento EE]' \
          '(-l --admin-location)'{-l,--admin-location}'[Install Magento EE]' \
          '--admin-pass[Specify the Magento admin password (default: randomly generated password)]' \
          '--admin-user[Specify the Magento admin username (default: admin)]' \
          '--admin-email[Specify the Magento admin email address (default: email of sitworx user)' \
          '(-a --auto-create-database)'{-a,--auto-create-database}'[Automatically create the database and user]' \
          '(-D --only-database)'{-D,--only-database}'[When used with -a, only the database is automatically created (not the user)]' \
          '(-p --package)'{-p,--package}'[URL, file path, or version number of Magento to install]' \
          '(-b --base-url)'{-b,--base-url}'[Specify the Magento base URL (default: auto-detected)]' \
          '(-s --secure-base-url)'{-s,--secure-base-url}'[Specify the Magento secure base URL]' \
          '(-d --install-sample-data)'{-d,--install-sample-data}'[Install the Magento sample data]' \
          '--database-host[Database host]' \
          '(-N --database-name)'{-N,--database-name}'[Database name. Default value: magento]' \
          '(-P --database-pass)'{-P,--database-pass}'[Database user password]' \
          '(-U --database-user)'{-U,--database-user}'[Database username. Default value: magento]' \
          '--database-port[Database port. Default value: 3306]' \
          '(-m --use-memcache)'{-m,--user-memcache}'[Create and use private memcache instances (requires root)]' \
          '(-y --assume-yes)'{-y,--assume-yes}'[Assume yes at prompts]' \
          '*:Path to install Magento to:_path_files -/'
        ;;

      "repair")
        _arguments \
          '(-p --set-permissions)'{-p,--set-permissions}'[Set file and directory permissions]' \
          '(-o --set-ownership)'{-o,--set-ownership}'[Set file and directory ownership (requires root access)]' \
          '(-c --clear-cache)'{-c,--clear-cache}'[Clear pear, image, and web cache]' \
          '--clear-sessions[Clear the saved sessions]' \
          '(-C --set-cache-method)'{-C,--set-cache-method}'[Set the Magento caching method]:Cache Storage Method:(file files apc memcached memcache redis)' \
          '(-S --set-session-storage)'{-S,--set-session-storage}'[Set the session storage location]:Session Storage Method:(files file db memcache memcached redis)' \
          '(-e --install-extension)'{-e,--install-extension}'[Install a Magento connect extension via key]' \
          '(-b --set-base-url)'{-b,--set-base-url}'[Set Magento’s base URL]' \
          '(-B --set-secure-base-url)'{-B,--set-secure-base-url}'[Set Magento’s secure base URL]' \
          '(-r --reset-admin-pass)'{-r,--reset-admin-pass}'[Reset a Magento admin user’s password (default: "admin" user and random password)]' \
          '(-i --reindex)'{-i,--reindex}'[Comma separated list of indexes to re-index or special word "all"]' \
          '--cdn-base-url[Base URL for the CDN (ex: http://cdn.example.com/), use "reset" to reset to default values (no CDN)]' \
          '--secure-cdn-base-url[Like --cdn-base-url but for secure CDN base URLs, use "reset" to reset to default values (no CDN)]' \
          '(-L --toggle-logging)'{-L,--toggle-logging}'[Toggle Magento’s internal logging function on or off]' \
          '(-l --log-rotation)'{-l,--log-rotation}'[Set the number of days to keep Magento database logs]' \
          '--toggle-log-rotation[Toggle log rotation of Magento’s log files]' \
          '(-R --set-return-path)'{-R,--set-return-path}'[Set the return-path email address for Magento emails]' \
          '--reset-db-pass[Reset the database user’s password to something random, update the local.xml, and clear the site cache]' \
          '*:Path to Magento:_path_files -/'
        ;;

      "copy")
        _arguments \
          '(-x --exclude-dirs)'{-x,--exclude-dirs}'[]' \
          '(-a --auto-create-database)'{-a,--auto-create-database}'[]' \
          '(-N --new-database-name)'{-N,--new-database-name}'[]' \
          '(-U --new-database-user)'{-U,--new-database-user}'[]' \
          '(-P --new-database-pass)'{-P,--new-database-pass}'[]' \
          '(-b --new-base-url)'{-b,--new-base-url}'[]' \
          '(-y --assume-yes)'{-y,--assume-yes}'[]' \
          '*::_path_files -/'
        ;;

      "link")
        _arguments \
          '*::_path_files -/'
        ;;

      "dbrepair")
        _arguments \
          '--setup-only[]' \
          '(-b --backup)'{-b,--backup}'[]' \
          '(-r --restore-on-error)'{-r,--restore-on-error}'[]' \
          '(-y --assume-yes)'{-y,--assume-yes}'[]' \
          '(-c --change-log)'{-c,--change-log}'[]' \
          '*::_path_files -/'
        ;;

      "backup")
        _arguments \
          '(-o --output)'{-o,--output}'[]' \
          '(-d --database-only)'{-d,--database-only}'[]' \
          '(-f --files-only)'{-f,--files-only}'[]' \
          '(-F --force)'{-f,--force}'[]' \
          '*::_path_files -/'
        ;;

      "crondump")
        _arguments \
          "(-s --start-time)"{-s,--start-time}"[]" \
          "(-e --end-time)"{-e,--end-time}"[]" \
          "(-f --format)"{-f,--format}"[]:Format type:(csv table)" \
          "(-d --delimiter)"{-d,--delimiter}"[]" \
          "(-H --hide-header)"{-H,--hide-header}"[]" \
          "(-R --repeat-header)"{-R,--repeat-header}"[]" \
          '*::_path_files -/'
        ;;

      "*")
        ;;
    esac
  else
    _describe -t commands " Sub commands " mainopts
  fi
}
compdef _nkmagento nkmagento
