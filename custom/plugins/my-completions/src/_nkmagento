#compdef nkmagento

_nkmagento() {
  local curcontext="$curcontext" state
  local -a mainopts

  mainopts=(
  'info: Show Informatin '
  'install: Install Magento'
  'repair: Repair/modify installation'
  'copy: Copy this site to a new one'
  'link: Create multistore symlinks'
  'dbrepair: Repair database'
  'backup: Backup this installation'
  'crondump: Show cron info'
  );

  _arguments -C \
    '1: :->command'\
    '*: :->args'

  if [[ "$state" == "args" ]]; then
    case "${words[2]}" in
      "info")
        _arguments \
          '*:Magento Directory:_path_files -/'
        ;;

      "install")
        _arguments \
          '(-E --enterprise)'{-E,--enterprise}'[Enterprise edition]' \
          '(-l --admin-location)'{-l,--admin-location}'[Admin location]' \
          '--admin-pass[Admin pass]' \
          '--admin-user[Admin user]' \
          '--admin-email[Admin email]' \
          '(-a --auto-create-database)'{-a,--auto-create-database}'[Auto create database]' \
          '(-D --only-database)'{-D,--only-database}'[Only create database]' \
          '(-p --package)'{-p,--package}'[Package name/version]' \
          '(-b --base-url)'{-b,--base-url}'[Base URL]' \
          '(-s --secure-base-url)'{-s,--secure-base-url}'[Secure base URL]' \
          '(-d --install-sample-data)'{-d,--install-sample-data}'[Install sample data]' \
          '--database-host[Database host]' \
          '(-N --database-name)'{-N,--database-name}'[Database name]' \
          '(-P --database-pass)'{-P,--database-pass}'[Database user password]' \
          '(-U --database-user)'{-U,--database-user}'[Database username]' \
          '--database-port[Database port]' \
          '(-m --use-memcache)'{-m,--user-memcache}'[Use memcached]' \
          '(-y --assume-yes)'{-y,--assume-yes}'[Assume yes]' \
          '*:Magento Directory:_path_files -/'
        ;;

      "repair")
        _arguments \
          '(-p --set-permissions)'{-p,--set-permissions}'[Set permissions]' \
          '(-o --set-ownership)'{-o,--set-ownership}'[Set ownership]' \
          '(-c --clear-cache)'{-c,--clear-cache}'[Clear cache]' \
          '--clear-sessions[Clear sessions]' \
          '(-C --set-cache-method)'{-C,--set-cache-method}'[Set cache storage method]:Cache Storage Method:(file files apc memcached memcache redis)' \
          '(-S --set-session-storage)'{-S,--set-session-storage}'[Set session storage method]:Session Storage Method:(files file db memcache memcached redis)' \
          '(-e --install-extension)'{-e,--install-extension}'[Install extension]' \
          '(-b --set-base-url)'{-b,--set-base-url}'[Set base url]' \
          '(-B --set-secure-base-url)'{-B,--set-secure-base-url}'[Set secure base url]' \
          '(-r --reset-admin-pass)'{-r,--reset-admin-pass}'[Reset admin pass]' \
          '(-i --reindex)'{-i,--reindex}'[Reindex]' \
          '--cdn-base-url[Set CDN base url]' \
          '--secure-cdn-base-url[Set secure CDN base url]' \
          '(-L --toggle-logging)'{-L,--toggle-logging}'[Toggle logging]' \
          '(-l --log-rotation)'{-l,--log-rotation}'[Log rotation]' \
          '--toggle-log-rotation[Toggle log rotation]' \
          '(-R --set-return-path)'{-R,--set-return-path}'[Set mail return path]' \
          '--reset-db-pass[Reset database password]' \
          '*:Magento Directory:_path_files -/'
        ;;

      "copy")
        _arguments \
          '(-x --exclude-dirs)'{-x,--exclude-dirs}'[Exclude directories]' \
          '(-a --auto-create-database)'{-a,--auto-create-database}'[Create database]' \
          '(-N --new-database-name)'{-N,--new-database-name}'[New database name]' \
          '(-U --new-database-user)'{-U,--new-database-user}'[New database user]' \
          '(-P --new-database-pass)'{-P,--new-database-pass}'[New database password]' \
          '(-b --new-base-url)'{-b,--new-base-url}'[New base URL]' \
          '(-y --assume-yes)'{-y,--assume-yes}'[Assume yes]' \
          '*:Magento Directory:_path_files -/'
        ;;

      "link")
        _arguments \
          '*:Magento Directory:_path_files -/'
        ;;

      "dbrepair")
        _arguments \
          '--setup-only[Setup only]' \
          '(-b --backup)'{-b,--backup}'[Backup]' \
          '(-r --restore-on-error)'{-r,--restore-on-error}'[Restore on error]' \
          '(-y --assume-yes)'{-y,--assume-yes}'[Assume yes]' \
          '(-c --change-log)'{-c,--change-log}'[Change log]' \
          '*:Magento Directory:_path_files -/'
        ;;

      "backup")
        _arguments \
          '(-o --output)'{-o,--output}'[Output]' \
          '(-d --database-only)'{-d,--database-only}'[Database only]' \
          '(-f --files-only)'{-f,--files-only}'[Files only]' \
          '(-F --force)'{-f,--force}'[Force]' \
          '*:Magento Directory:_path_files -/'
        ;;

      "crondump")
        _arguments \
          "(-s --start-time)"{-s,--start-time}"[Start time]" \
          "(-e --end-time)"{-e,--end-time}"[End time]" \
          "(-f --format)"{-f,--format}"[Output Format]:Format type:(csv table)" \
          "(-d --delimiter)"{-d,--delimiter}"[Delimiter]" \
          "(-H --hide-header)"{-H,--hide-header}"[Hide header]" \
          "(-R --repeat-header)"{-R,--repeat-header}"[Repeat header]" \
          '*:Magento Directory:_path_files -/'
        ;;

      "*")
        ;;
    esac
  else
    _describe -t commands " Sub commands " mainopts
  fi
}
compdef _nkmagento nkmagento
